#!/usr/bin/python3

from SCons.Script import AddOption, Default, Depends, Dir, GetOption, Import, Return

Import("GlobalEnv")

RTOS_DIR = Dir("RTOS")
SRC_DIR = Dir("src")
HW_DIR = Dir("HW")
LIBS_DIR = Dir("#/embedded/libs")
FREERTOS_DIR = LIBS_DIR.Dir("FreeRTOS")
CMSIS_DIR = LIBS_DIR.Dir("CMSIS")

BUILD_DIR = Dir("build")

APP_START_ADDR = 0x08000000

env = GlobalEnv.Clone(
    tools=["gcc-arm-none-eabi", "chip_config", "st-flash", "openocd", "compilation_db"],
    BUILD_DIR=BUILD_DIR,
)
rtos_env = GlobalEnv.Clone(tools=["gcc-arm-none-eabi"])

env["OPENOCD_INTERFACE"] = "stlink"
env["OPENOCD_MCU"] = "stm32f103c8"


c_flags = [
    "-std=c11",
    "-mthumb",
    "-mcpu=cortex-m3",
    "-mfloat-abi=soft",
    "-msoft-float",
    # "-nostdlib",
    # "-nostartfiles", # uncomment after porting start files from bootloader
    "--specs=nano.specs",
    "--specs=nosys.specs",
    "-ffunction-sections",
    "-fdata-sections",
    "-fshort-enums",
    "-funsigned-char",
    "-DSTM32F1",
    "-DSTM32F103xB",
    "-DUSE_HAL_DRIVER",
    "-Wall",
    "-Wextra",
    "-Werror",
    "-Wfloat-equal",
    "-Wcast-align",
    "-Wlogical-op",
    "-Winline",
    "-Wshadow",
    "-Winit-self",
    "-Wmissing-prototypes",
    "-Wunused-function",
    "-Wpointer-arith",
    "-Wno-type-limits",
    "-Wno-unused-local-typedefs",
]

debug_flags = [
    "-ggdb3",
    "-g3",
    "-Og",
]

release_flags = [
    "-O2",
]

if GetOption("release"):
    c_flags += release_flags
else:
    c_flags += debug_flags

link_flags = [
    "-Wl,--gc-sections",
    # "-Wl,--print-gc-sections",
    "-Wl,--relax",
    "-Wl,--print-memory-usage",
]

as_flags = [
    # tell the assembler to run the c preprocessor on assembly files
    "-xassembler-with-cpp"
]

free_rtos_flags = [
    "-Wno-missing-prototypes",
    "-Wno-cast-align",
]

chip_config = env.ChipConfig(config_file="HW/mcuConfig.yaml")
c_flags.extend(chip_config.get("defines", []))


RTOS_INCLUDE_DIRS = [
    RTOS_DIR.Dir("include"),
    FREERTOS_DIR.Dir("Source/include/"),
    FREERTOS_DIR.Dir("Source/portable/GCC/ARM_CM3/"),
    CMSIS_DIR.Dir("CMSIS/Core/Include/"),
]

DRIVERS_INCLUDE_DIRS = [
    chip_config["cmsis_includes"],
    chip_config["hal_includes"],
]

LIBS_INCLUDE_DIRS = [
    LIBS_DIR.Dir("printf"),
    LIBS_DIR.Dir("atomic"),
]

PROJECT_INCLUDE_DIRS = [
    "include/",
    "HW/include/",
    "generated/",
    "lib/lib_FTDI_FT81x/include",
]

paths = (
    RTOS_INCLUDE_DIRS + DRIVERS_INCLUDE_DIRS + PROJECT_INCLUDE_DIRS + LIBS_INCLUDE_DIRS
)

env.Append(
    ASFLAGS=as_flags,
    CPPPATH=paths,
    CCFLAGS=c_flags,
    LINKSCRIPT=chip_config["linker_file"],
    LINKFLAGS=c_flags + link_flags,
    MAPFILE=BUILD_DIR.File('steering_wheel.map'),
)

rtos_env.Append(
    CPPPATH=paths,
    CCFLAGS=c_flags + free_rtos_flags,
)

# List of component C files

src_files = [
    SRC_DIR.File("SystemManager.c"),
    SRC_DIR.File("Module.c"),
    SRC_DIR.File("CAN/CANIO-tx.c"),
    SRC_DIR.File("CAN/CANIO-rx.c"),
    SRC_DIR.File("IO.c"),
    SRC_DIR.File("Screen.c"),
    SRC_DIR.File("Display/Pills.c"),
    SRC_DIR.File("Display/Dots.c"),
]

hw_src_files = [
    HW_DIR.File("HW_adc.c"),
    HW_DIR.File("HW_can.c"),
    HW_DIR.File("HW_dma.c"),
    HW_DIR.File("HW_gpio.c"),
    HW_DIR.File("HW_intc.c"),
    HW_DIR.File("HW_msp.c"),
    HW_DIR.File("HW_spi.c"),
    HW_DIR.File("HW_timebase.c"),
]

ftdi_lib_files = [
    Dir("lib").File("lib_FTDI_FT81x/src/EVE_commands.c"),
    Dir("lib").File("lib_FTDI_FT81x/src/EVE_target.c"),
]

src_files += hw_src_files + ftdi_lib_files

rtos_src_files = [
    RTOS_DIR.File("FreeRTOSResources.c"),
    RTOS_DIR.File("FreeRTOS_SWI.c"),
    FREERTOS_DIR.File("Source/croutine.c"),
    FREERTOS_DIR.File("Source/event_groups.c"),
    FREERTOS_DIR.File("Source/list.c"),
    FREERTOS_DIR.File("Source/queue.c"),
    FREERTOS_DIR.File("Source/tasks.c"),
    FREERTOS_DIR.File("Source/timers.c"),
    FREERTOS_DIR.File("Source/portable/GCC/ARM_CM3/port.c"),
    # FREERTOS_DIR.File("Source/stream_buffer.c"),
    # FREERTOS_DIR.File("Source/portable/MemMang/heap_4.c"),
]

platform_source_files = [
    (
        LIBS_DIR.File("printf/printf.c"),
        ["-Wno-float-equal", "-DPRINTF_INCLUDE_CONFIG_H"],
    ),
]

# add driver source files
platform_source_files.extend(chip_config["sources"])


def create_output_file_path(file, new_ext, target_dir=None) -> str:
    fn = file.target_from_source(prefix="", suffix=new_ext).name

    if target_dir:
        return BUILD_DIR.Dir(target_dir).File(fn)

    return BUILD_DIR.Dir("/".join(file.path.split("/")[2:-1]) + "/").File(fn)


def compile_objects(env, files, extra_flags=[], target_dir=None):
    objs = []
    for i, src_file in enumerate(files):
        target = create_output_file_path(src_file, ".obj", target_dir)
        flags = extra_flags[i] if extra_flags else []
        objs.append(
            env.Object(target=target, source=src_file, CCFLAGS=env["CCFLAGS"] + flags)
        )
    return objs


## actually compile stuff

objs = []  # store objects here

# compile platform files
objs.extend(
    compile_objects(env, *list(zip(*platform_source_files)), target_dir="embedded/")
)

# compile rtos source files
objs.extend(compile_objects(rtos_env, rtos_src_files, target_dir="FreeRTOS/"))

# compile project source files
objs.extend(compile_objects(env, src_files))

# link all the compiled objects into an elf
elf, map = env.Program(BUILD_DIR.File("steering_wheel.elf"), objs)
Depends(elf, env["LINKSCRIPT"])

# generate a binary for flashing
binary = env.Bin(source=elf)
Default(binary)

# Generate compile_commands.json
compile_db = env.CompilationDatabase(BUILD_DIR.File("compile_commands.json"))
Depends(compile_db, objs)
# generate it by default
Default(compile_db)
# add an alias to _only_ generate the compilation database
env.Alias("compiledb", compile_db)

# Return artifacts to the caller
artifacts = {
    "FLASHABLE_ARTIFACT": {
        "artifact": binary,
        "addr": APP_START_ADDR,
        "tools": ["st-flash"],
    },
    "DEBUG_ARTIFACT": {
        "artifact": elf,
        "args": [],
        "tools": ["gcc-arm-none-eabi", "openocd"],
        "env": {
            "OPENOCD_INTERFACE": "stlink",
            "OPENOCD_MCU": "stm32f103c8",
        },
    },
}

Return("artifacts")
